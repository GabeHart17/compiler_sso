PROGRAM ::= FUNCTION PROGRAM | VAR_DECL PROGRAM | t_eof
FUNCTION ::= VAR_DECL t_paren_left F_DECL_ARGS t_paren_right BLOCK
VAR_DECL ::= TYPE_NAME t_ident
F_DECL_ARGS ::= ARG_DECLS | epsilon
ARG_DECLS ::= VAR_DECL t_coma ARG_DECLS | VAR_DECL
BLOCK ::= t_curly_left STATEMENTS t_curly_right | STATEMENT t_semicolon
STATEMENTS ::= STATEMENT STATEMENTS | epsilon
STATEMENT ::= EXPR t_semicolon

EXPR ::= E12
E0 ::= t_paren_left EXPR t_paren_right | R_VALUE
E1 ::= t_increment E1 | t_decrement E1 | t_not E1 | t_bitwise_not E1 | t_cast E1 | t_dereference E1 | t_reference E1 | E0
E2 ::= E1 t_multiply E2 | E1 t_divide E2 | E1 t_modulo E2 | E1
E3 ::= E2 t_plus E3 | E2 t_minus E3 | E2
E4 ::= E3
E5 ::= E4 t_less_than E5 | E4 t_greater_than E5 | E4 t_lt_or_eq E5 | E4 t_gt_or_eq E5 | E4
E6 ::= E5 t_equal E6 | E5 t_not_equal E6 | E5
E7 ::= E6 t_bitwise_and E7 | E6
E8 ::= E7
E9 ::= E8 t_bitwise_or E9 | E8
E10 ::= E9 t_and E10 | E9
E11 ::= E10 t_or E11 | E10
E12 ::= E11 t_assign E12 | E11

TYPE_NAME ::= t_bool | t_char | t_float | t_int | t_pointer
R_VALUE ::= t_literal_int | t_literal_float | t_ident
